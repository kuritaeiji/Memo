# モジュール

モジュールは汎用言語の関数と同様のものと考える。関数と同様に以下の機能がある。

- 変数 variable（引数）
- ローカルな値 locals（ローカル変数）
- 出力変数 output（返り値）

## 変数 variable

モジュール呼び出し時に設定可能な変数（引数）を定義できる。

モジュール

```main.tf
variable "cluster-name" {
  type = string
}

resource "aws_autoscaling_group" "cluster" {
  name = "${var.cluster-name}-cluster"
}
```

モジュール呼び出し元

```main.tf
module "asg" {
  source = "../../modules/asg"

  cluster-name = "Webアプリ"
}
```

## ローカルな値 locals

モジュール内でのみ使用可能なローカル変数を定義できる。

```main.tf
locals {
  http_port = 80
  tcp_protocol = "tcp"
  all_ips = ["0.0.0.0/0"]
}

resource "aws_security_group" "alb" {
  name = "alb"

  ingress {
    from_port = local.http_port
    to_port = local.http_port
    protocol = local.tcp_protocol
    cidr_blocks = local.all_ips
  }
}
```

## 出力変数 output

モジュール実行後に呼び出し元に返却できる値（返り値）を定義できる。

モジュール

```main.tf
resource "aws_lb" "app" {
  name = "app"
}

output "alb_dns_name" {
  value = aws_lb.app.dns_name
}
```

モジュール呼び出し元

```main.tf
module "alb" {
  source = "../../modules/alb"
}

output "alb_dns_name" {
  value = module.alb.alb_dns_name
}
```

## ファイルパス

Terraform はコマンド実行時のカレントディレクトリに対する相対パスでファイルを指定する。モジュールからパスを指定する場合はパス参照を使用する。

- path.module: モジュールが存在するディレクトリを返す
- path.root: ルートモジュールが存在するディレクトリを返す

```main.tf
user_data = templatefile("${path.module}/user-data.sh", {
  server_port = var.server_port
})
```

## モジュールのバージョン管理

モジュール呼び出し時にパスを指定するだけでなく、Git リポジトリを使用することができる。各コミットにバージョン番号の Git タグを付与することで環境ごとに異なるバージョンのモジュールを使用できる。

modules ディレクトリを Git リポジトリにし、Git タグを付与する。

```bash
cd modules
git init
git add .
git commit -m '初回コミット'
git remote add origin <URL>
git push origin main
git tag -a 'v0.0.1' -m '初回コミット'
git push --follow-tags
```

モジュール呼び出し時に Github リポジトリ・パス・Git タグを指定する。

```main.tf
module "asg" {
  source = "github.com/kuritaeiji/terraform-module//modules/cluster/asg-rolling-deploy?ref=v0.0.1"
}
```
