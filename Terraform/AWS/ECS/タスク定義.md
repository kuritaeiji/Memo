# タスク定義

- タスク定義
  - family 名
  - cpu・メモリ
  - ネットワークモード
  - 起動可能なサーバータイプ指定
  - タスク実行ロール
  - タスクロール
  - コンテナ定義
    - コンテナ名
    - イメージ名
    - ログ設定
    - ポートマッピング
- ロググループ
- ECR と CloudWatchLogs に関するマネージドポリシーデータソース
- ssm パラメーターに対するポリシードキュメント
- タスク実行ロール

```main.tf
resource "aws_ecs_task_definition" "backend" {
  # familyにはタスク定義のプレフィックスを指定する
  # タスク定義名はファミリーにリビジョンを付与したもの 例) backend:1
  family = "backend"
  cpu = "256"
  memory = "512"
  # awsvpcはタスクごとに個別のENIが付与される
  # 配置されるサブネットのIPアドレスが付与される
  network_mode = "awsvpc"
  # タスクを起動可能な起動タイプ（FARGATEまたはEC2を選択できる）
  # 実際にタスクを起動するタイプの選択はServiceで実施する。ここではServiceで選択できる起動タイプを指定する
  requires_compatibilities = ["FARGATE"]
  # コンテナ定義
  container_definitions = jsonencode([
    {
      name = "backend"
      image = "nginx:latest"
      essential = true
      logConfiguration = {
        logDriver = "awslogs",
        options = {
          awslogs-region = "ap-northeast-1"
          awslogs-stream-prefix = "nginx"
          awslogs-group = "/ecs/examples"
        }
      },
      portMappings = [
        {
          protocol = "tcp"
          containerPort = 80
        }
      ]
    }
  ])

  # ECSコンテナエージェントのRole
  execution_role_arn = module.ecs_task_execution_role.iam_role_arn
}

# ロググループ作成
resource "aws_cloudwatch_log_group" "for_ecs" {
  name = "/ecs/examples"
  retention_in_days = 180
}

# ECRとCloudWatchLogsのマネージドポリシー
data "aws_iam_policy" "ecs_task_execution_role_policy" {
  arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

# ssmパラメータからインフラパラメーターを取得するためのポリシードキュメント
data "aws_iam_policy_document" "ecs_task_execution" {
  source_policy_documents = [data.aws_iam_policy.ecs_task_execution_role_policy.policy]

  statement {
    effect = "Allow"
    actions = ["ssm:GetParameters", "kms:Decrypt"]
    resources = ["*"]
  }
}

# タスク実行ロール
module "ecs_task_execution_role" {
  source = "../../modules/iam-role"

  name = "backend-ecs-task-execution-role"
  identifiers = ["ecs-tasks.amazonaws.com"]
  policy_document_json = data.aws_iam_policy_document.ecs_task_execution.json
  policy_name = "backend-ecs-task-execution-policy"
}
```
